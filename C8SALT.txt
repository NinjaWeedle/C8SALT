:"TI-BASIC CHIP8 EMU
DelVar MDelVar JDelVar thetaAxesOff
If 11>real(11
Then
	Disp "DISABLE XLIB
	Stop
End
Lbl A
ClrHome
Menu("C8SALT BY OXITI8","RESUME",1,"LOAD FROM |LROM",2,"SETTINGS",3,"CLEAR RPL DATA",4,"EXIT",5
Lbl 5
Stop
Lbl 4
DelVar |LRPLPause "RPL FLAGS RESET
Goto A
Lbl 3
Input "HLE FONT?",theta
Input "VIP FX55/65?",J
Input "VIP SHIFTS?",M
Goto A
Lbl 2
Output(4,5,"LOADING...
64->W
W->B
8W->P
32->L
ClrList L2,|LSTK,L1
P->dim(L1
DelVar ZSetUpEditor FONT
For([recursiven],1,dim(|LFONT),2
	Z+1->Z
	|LFONT([recursiven])+[i]|LFONT([recursiven]+1->L1(Z
End
SetUpEditor ROM,RPL
If 2fPart(.5dim(|LROM
1+dim(|LROM->dim(|LROM
For([recursiven],1,dim(|LROM),2
	B+1->B
	|LROM([recursiven])+[i]|LROM([recursiven]+1->L1(B
End
Archive |LROM
16->dim(L2
Ans->dim(|LRPL
DelVar IDelVar BDelVar SDelVar QOutput(5,2,"ALL SYSTEMS GO
1->dim(|LSTK
ClrDraw
Lbl 1
Xmin+|E2DeltaX>Xmax
Text(~1,56(2-Ans),60(2-Ans),"C8SALT
Repeat getKey=22
	L1(1+int(P/8->A
	8fPart(P/8->[recursiven]
	If [recursiven]=7
	Then
		iPart(1000fPart(|E9imag(A->C
		iPart(1000real(L1(2+int(P/8->D
	End
	If [recursiven]=6
	Then
		iPart(1000fPart(|E6imag(A->C
		iPart(1000fPart(|E9imag(A->D
	End
	If [recursiven]=5
	Then
		1000fPart(.001iPart(|E6imag(A->C
		iPart(1000fPart(|E6imag(A->D
	End
	If [recursiven]=4
	Then
		iPart(1000imag(A->C
		1000fPart(.001iPart(|E6imag(A->D
	End
	If [recursiven]=1
	Then
		1000fPart(.001iPart(|E6real(A->C
		iPart(1000fPart(|E6real(A->D
	End
	If [recursiven]=2
	Then
		iPart(1000fPart(|E6real(A->C
		iPart(1000fPart(|E9real(A->D
	End
	If [recursiven]=3
	Then
		iPart(1000fPart(|E9real(A->C
		iPart(1000imag(A->D
	End
	If not([recursiven]
	Then
		iPart(1000real(A->C
		1000fPart(.001iPart(|E6real(A->D
	End
	P+2->P
	prgmC8CPU
	If Q
	Q-1->Q
	If S
	S-1->S
End
DelVar L1ClrHome
Disp "PC:
Output(1,5,P
Disp "I:
Output(2,4,I
Disp "DT:
Output(3,5,Q
Disp "ST:
Output(4,5,S
Disp "OP1:
Output(5,6,C
Disp "OP2:
Output(6,6,D
Pause "PRESS ENTER