:"TI-BASIC CHIP8 EMU
DelVar MDelVar JDelVar thetaAxesOff
If 11>real(11
Then
	Disp "DISABLE XLIB
	Stop
End
Lbl A
ClrHome
Menu("C8SALT BY OXITI8","RESUME",1,"LOAD FROM |LROM",2,"SETTINGS",3,"CLEAR RPL FLAGS",4,"EXIT",5
Lbl 5
Stop
Lbl 4
DelVar |LRPLPause "RPL FLAGS RESET
Goto A
Lbl 3
Input "HLE FONT?",theta
Input "VIP FX55/65?",J
Input "VIP SHIFTS?",M
Goto A
Lbl 2
Output(4,5,"LOADING...
64->W
W->B
8W->P
32->L
ClrList |LREG,|LSTK,|LRAM
P->dim(|LRAM
If theta
Then
	DelVar ZSetUpEditor FONT
	For([recursiven],1,dim(|LFONT),2
		Z+1->Z
		|LFONT([recursiven])+[i]|LFONT([recursiven]+1->|LRAM(Z
	End
End
SetUpEditor ROM,RPL
If 2fPart(.5dim(|LROM
1+dim(|LROM->dim(|LROM
For([recursiven],1,dim(|LROM),2
	B+1->B
	|LROM([recursiven])+[i]|LROM([recursiven]+1->|LRAM(B
End
Archive |LROM
16->dim(|LREG
16->dim(|LRPL
DelVar IDelVar BDelVar SDelVar QOutput(5,2,"ALL SYSTEMS GO
1->dim(|LSTK
ClrDraw
Lbl 1
Xmin+|E2DeltaX>Xmax
Text(~1,56(2-Ans),60(2-Ans),"C8SALT
Repeat getKey=22
	|LRAM(1+int(P/8->A
	8fPart(P/8->B
	If B>3
	Then
		If B=6
		Then
			iPart(1000fPart(10|E5imag(A->C
			iPart(1000fPart(10|E8imag(A->D
		Else
			iPart(1000imag(A->C
			1000fPart(.001iPart(10|E5imag(A->D
		End
	Else
		If B
		Then
			iPart(1000fPart(10|E5real(A->C
			iPart(1000fPart(10|E8real(A->D
		Else
			iPart(1000real(A->C
			1000fPart(.001iPart(10|E5real(A->D
		End
	End
	P+2->P
	prgmC8CPU
	If Q
	Q-1->Q
	If S
	S-1->S
End
DelVar |LRAMClrHome
Disp "PC:
Output(1,5,P
Disp "I:
Output(2,4,I
Disp "DT:
Output(3,5,Q
Disp "ST:
Output(4,5,S
Disp "OP1:
Output(5,6,C
Disp "OP2:
Output(6,6,D