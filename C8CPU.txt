If not(C
Then
	If D=238
	Then
		|LSTK(dim(|LSTK->P
		dim(|LSTK)-1->dim(|LSTK
		Return
	End
	If D=253
	Stop
	If D>223
	ClrDraw
	If D=254
	Then
		32->L
		64->W
		Return
	End
	If D=255
	Then
		If not(Xmin+|E2DeltaX>Xmax
		Then
			64->L
			128->W
		Else
			62->L
			92->W
		End
	End
	Return
End
If C<32
Then
	D+4096fPart(C/16->P
	Return
End
If C<48
Then
	1+dim(|LSTK->dim(|LSTK
	P->|LSTK(dim(|LSTK
	D+4096fPart(C/16->P
	Return
End
If C<64
Then
	P+2(D=L2(1+16fPart(C/16->P
	Return
End
If C<80
Then
	P+2(D!=L2(1+16fPart(C/16->P
	Return
End
If C<96
Then
	P+2(L2(1+16fPart(C/16))=L2(1+int(D/16->P
	Return
End
If C<112
Then
	D->L2(1+16fPart(C/16
	Return
End
If C<128
Then
	D+L2(1+16fPart(C/16
	256fPart(Ans/256->L2(1+16fPart(C/16
	Return
End
If C<144
Then
	16fPart(D/16->A
	L2(1+int(D/16->H
	L2(1+16fPart(C/16->G
	If not(A
	H->L2(1+16fPart(C/16
	0->F
	If A=1
	Then
		For(B,0,8
			round(e^(Bln(2)),0->E
			F+E(0<((2fPart(int(G/E)/2))+2fPart(.5int(H/E->F
		End
		F->L2(1+16fPart(C/16
		Return
	End
	If A=2
	Then
		For(B,0,8
			round(e^(Bln(2)),0->E
			F+E(1<((2fPart(int(G/E)/2))+2fPart(.5int(H/E->F
		End
		F->L2(1+16fPart(C/16
		Return
	End
	If A=3
	Then
		For(B,0,8
			round(e^(Bln(2)),0->E
			F+E(2fPart(.5((2fPart(int(G/E)/2))+2fPart(.5int(H/E->F
		End
		F->L2(1+16fPart(C/16
		Return
	End
	If A=4
	Then
		H+G->B
		B>255->L2(16
		256fPart(B/256->L2(1+16fPart(C/16
		Return
	End
	If A=5
	Then
		G-H->B
		B>=0->E
		If not(E
		B+256->B
		256fPart(B/256->L2(1+16fPart(C/16
		E->L2(16
		Return
	End
	If A=6
	Then
		If M
		Then
			int(.5H->L2(1+16fPart(C/16
		Else
			int(.5G->L2(1+16fPart(C/16
		End
		2fPart(.5G->L2(16
		Return
	End
	If A=7
	Then
		H-G->B
		B>=0->E
		If not(E
		B+256->B
		256fPart(B/256->L2(1+16fPart(C/16
		E->|LREG(16
		Return
	End
	If A=14
	Then
		If M
		Then
			256fPart(2H/256->L2(1+16fPart(C/16
		Else
			256fPart(2G/256->L2(1+16fPart(C/16
		End
		128<=G->L2(16
	End
	Return
End
If C<160
Then
	P+2(L2(1+(16fPart(C/16)))!=L2(1+(int(D/16->P
	Return
End
If C<176
Then
	D+4096fPart(C/16->I
	Return
End
If C<192
Then
	D+4096fPart(C/16)+L2(1->P
	Return
End
If C<208
Then
	0->B
	randInt(0,255->R
	For([recursiven],0,8
		round(e^([recursiven]ln(2)),0
		B+(1<2fPart(.5int(D/Ans))+2fPart(.5int(R/Ans->B
	End
	B->L2(1+16fPart(C/16
	Return
End
If C<224
Then
	0->[recursiven]
	L2(1+(int(D/16->H
	If I<420 and not(theta
	Then
		If I<16
		Text(1+LfPart(H/L),1+WfPart(L2(1+16fPart(C/16))/W),sub("0123456789ABCDEF",I+1,1
	Else
		I->E
		For(O,1,16fPart(D/16
			L2(1+(16fPart(C/16->G
			1+int(E/8->A
			8fPart(E/8->B
			If B=7
			iPart(1000fPart(|E9imag(L1(A->R
			If B=6
			iPart(1000fPart(|E6imag(L1(A->R
			If B=5
			1000fPart(.001iPart(|E6imag(L1(A->R
			If B=4
			iPart(1000imag(L1(A->R
			If B=3
			iPart(1000fPart(|E9real(L1(A->R
			If B=2
			iPart(1000fPart(|E6real(L1(A->R
			If B=1
			1000fPart(.001iPart(|E6real(L1(A->R
			If not(B
			iPart(1000real(L1(A->R
			For(N,1,8
				If R>127
				Then
					If pxl-Test(1+LfPart(H/L),1+WfPart(G/W
					1->[recursiven]
					Pxl-Change(1+LfPart(H/L),1+WfPart(G/W
				End
				256fPart((2R)/256->R
				G+1->G
			End
			H+1->H
			E+1->E
		End
	End
	[recursiven]->L2(16
	Return
End
If C<240
Then
	~1->K
	getKey
	If Ans=103
	0->K
	If Ans=72
	1->K
	If Ans=73 or Ans=25
	2->K
	If Ans=74
	3->K
	If Ans=82 or Ans=24
	4->K
	If Ans=83 or Ans=34
	5->K
	If Ans=84 or Ans=26
	6->K
	If Ans=92
	7->K
	If Ans=93
	8->K
	If Ans=94
	9->K
	If Ans=102
	10->K
	If Ans=104
	11->K
	If Ans=75
	12->K
	If Ans=85
	13->K
	If Ans=95
	14->K
	If Ans=105
	15->K
	If D=158
	Then
		P+2(K=L2(1+16fPart(C/16->P
	Else
		P+2(K!=L2(1+16fPart(C/16->P
	End
	Return
End
If D=7
Then
	Q->L2(1+16fPart(C/16
	Return
End
If D=10
Then
	0->K
	Repeat Ans>71 and 1<10fPart(Ans/10
		If Q
		Q-1->Q
		getKey
	End
	If Ans=72
	1->K
	If Ans=73 or Ans=25
	2->K
	If Ans=74
	3->K
	If Ans=82 or Ans=24
	4->K
	If Ans=83 or Ans=34
	5->K
	If Ans=84 or Ans=26
	6->K
	If Ans=92
	7->K
	If Ans=93
	8->K
	If Ans=94
	9->K
	If Ans=102
	10->K
	If Ans=104
	11->K
	If Ans=75
	12->K
	If Ans=85
	13->K
	If Ans=95
	14->K
	If Ans=105
	15->K
	K->L2(1+16fPart(C/16
	Return
End
If D=21
Then
	L2(1+16fPart(C/16->Q
	Return
End
If D=24
Then
	L2(1+16fPart(C/16->S
	Return
End
If D=30
Then
	4096fPart((I+L2(1+16fPart(C/16)))/4096->I
	Return
End
If D=41
Then
	(5^theta)L2(1+16fPart(C/16->I
	Return
End
If D=48
Then
	80+10^(theta)L2(1+16fPart(C/16->I
	Return
End
If D=51
Then
	For([recursiven],0,2
		1+int(.125(I+[recursiven]->A
		8fPart(.125(I+[recursiven]->B
		|E~12iPart(1000fPart(|E9imag(L1(A
		If B=6
		|E~9iPart(1000fPart(|E6imag(L1(A
		If B=5
		.001fPart(.001iPart(|E6imag(L1(A
		If B=4
		.001iPart(1000imag(L1(A
		If B=3
		|E~12iPart(1000fPart(|E9real(L1(A
		If B=2
		|E~9iPart(1000fPart(|E6real(L1(A
		If B=1
		.001fPart(.001iPart(|E6real(L1(A
		If not(B
		.001iPart(1000real(L1(A
		Ans->Z
		4fPart(.25B->E
		10fPart(.1L2(1+16fPart(C/16
		If [recursiven]=1
		10fPart(.1int(.1L2(1+16fPart(C/16
		If not([recursiven]
		int(.01L2(1+16fPart(C/16
		
		Ans10^(~3(E+1
		If B>3
		[i]Ans
		Ans-Z+L1(A->L1(A
	End
	Return
End
If D<102
Then
	For([recursiven],0,16fPart(C/16
		1+int(.125(I+[recursiven]->A
		8fPart(.125(I+[recursiven]->B
		|E~12iPart(1000fPart(|E9imag(L1(A
		If B=6
		|E~9iPart(1000fPart(|E6imag(L1(A
		If B=5
		.001fPart(.001iPart(|E6imag(L1(A
		If B=4
		.001iPart(1000imag(L1(A
		If B=3
		|E~12iPart(1000fPart(|E9real(L1(A
		If B=2
		|E~9iPart(1000fPart(|E6real(L1(A
		If B=1
		.001fPart(.001iPart(|E6real(L1(A
		If not(B
		.001iPart(1000real(L1(A
		Ans->Z
		4fPart(.25B->E
		If D=85
		Then
			L2([recursiven]+1)10^(~3(E+1
			If B>3
			[i]Ans
			Ans-Z+L1(A->L1(A
		End
		If D=101
		Z(10^(E3+3->L2([recursiven]+1
		If J
		I+[recursiven]+1->I
	End
	Return
End
If D=117
Then
	For([recursiven],0,16fPart(C/16
		|LRPL([recursiven]+1->L2([recursiven]+1
	End
	Return
End
If D=133
Then
	For([recursiven],0,16fPart(C/16
		L2([recursiven]+1->|LRPL([recursiven]+1
	End
End